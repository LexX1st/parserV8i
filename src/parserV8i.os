Перем ИмяФайлаСпискаБаз;

Функция ЗаписатьРезультатВФайл(ИмяФайла, Данные)
	Текст = Новый ЗаписьТекста(ИмяФайла);
	Текст.Записать(Данные);
	Текст.Закрыть();
КонецФункции // ЗаписатьРезультатВФайл(ИмяФайла,Данные)

Функция ТекстИниФайла(ИниФайл)
	Если Не ИниФайл.Существует() Тогда
		ВызватьИсключение "Не найден файл " + ИниФайл.ПолноеИмя;
	КонецЕсли;
	ТекстИни = Новый ЧтениеТекста(ИниФайл.ПолноеИмя, "utf-8");
	Текст = ТекстИни.Прочитать();
	Если ПустаяСтрока(Текст) Тогда
		ВызватьИсключение "Из файла ничего не прочитано"
	Иначе
		Возврат Текст;
	КонецЕсли;
КонецФункции

Функция ПолучитьФайлНастроекПоУмолчанию() Экспорт
	СИ = Новый СистемнаяИнформация;
	Окружение = СИ.ПеременныеСреды();
	Возврат Окружение.Получить("USERPROFILE") + "\appdata\roaming\1c\1cestart\ibases.v8i";
КонецФункции // ПолучитьФайлНастроекПоУмолчанию() Экспорт

/// Устанавливает файл парсинга
//
// Параметры:
//  Источник - Строка - Путь к файлу *.v8i. Если параметр не указан берется основной файл пользователя
//
Процедура УстановитьФайл(Источник="") Экспорт
	ИмяФайлаСпискаБаз = Источник;
	Если СокрЛП(ИмяФайлаСпискаБаз) = "" Тогда
		ИмяФайлаСпискаБаз = ПолучитьФайлНастроекПоУмолчанию();
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьФайл() Экспорт
	Возврат ИмяФайлаСпискаБаз;
КонецФункции

///  Получает список баз из указанного файла
//
// Возвращаемое значение:
//  Соответствие - Список баз, где: Ключ - имя базы, Значение: Структура с параметрами соединения
//
Функция ПолучитьСписокБаз() Экспорт

	ИмяФайлаНастроек = ПолучитьФайл();

	Данные = ТекстИниФайла(Новый Файл(ИмяФайлаНастроек));
	РегВыражениеСписокБаз = новый РегулярноеВыражение("\[[^\]\r\n]+](?:\r?\n(?:[^\[\r\n].*)?)*");
	РегВыражениеПереносСтрок = новый РегулярноеВыражение("\r\n");
	РегВыражениеЗначения = новый РегулярноеВыражение("(=|;)(?=(?:[^\""]*\""[^\""]*\"")*(?![^\""]*\""))");

	Базы = РегВыражениеСписокБаз.НайтиСовпадения(Данные);

	СписокБаз = Новый Соответствие;
	Для каждого База из Базы Цикл
		МассивСтр = РегВыражениеПереносСтрок.Разделить(База.Значение);
		СтруктураПути = Новый  Структура();
		Для каждого Стр из МассивСтр Цикл
			Если СокрЛП(Стр) = "" Тогда
				Продолжить;
			КонецЕсли;
			Если (Лев(Стр, 1) = "[") Тогда
				СтруктураПути.Вставить("Name",Сред(Стр,2,СтрДлина(Стр)-2));
			ИначеЕсли ВРег(Лев(Стр, 7)) = ВРег("Connect") Тогда
				СтрАдрес = Сред(Стр,9);
				РезАдрес  = РегВыражениеЗначения.Разделить(СтрАдрес);
				СтруктураСоединения = Новый Структура();
				Если РезАдрес.Количество() = 5 Тогда
					СтруктураСоединения.Вставить(РезАдрес[0],РезАдрес[2]);
				ИначеЕсли РезАдрес.Количество() = 9 Тогда
					СтруктураСоединения.Вставить(РезАдрес[0],РезАдрес[2]);
					СтруктураСоединения.Вставить(РезАдрес[4],РезАдрес[6]);
				КонецЕсли;

				СтруктураАдреса = Новый Структура;
				СтруктураАдреса.Вставить("String",Стр);
				СтруктураАдреса.Вставить("Structure",СтруктураСоединения);
				СтруктураПути.Вставить("Connect",СтруктураАдреса);
			Иначе
				ПозицияРавно = СтрНайти(Стр,"=");
				СтруктураПути.Вставить(Сред(Стр,1,ПозицияРавно-1),Сред(Стр,ПозицияРавно+1));
			КонецЕсли;

		КонецЦикла;
		СписокБаз.Вставить(СтруктураПути.Name,СтруктураПути);
	КонецЦикла;

	Возврат СписокБаз;
КонецФункции

///  Сохраняет данные списка баз
//
// Параметры:
//  ИмяИНИФайла - Строка - Имя файла для записи списка баз;
//  СписокБаз   - Соответствие - Список баз (получаются через ПолучитьСписокБаз).
//
// Возвращаемое значение:
//  Булево - Пока заглушка, всегда Истина
//
Функция ЗаписатьСписокБаз(СписокБаз, ИмяФайлаНастроек = "") Экспорт

	Если ИмяФайлаНастроек = "" Тогда
		ИмяФайлаНастроек = ПолучитьФайл();
	КонецЕсли;

	Данные = "";
	Для каждого База Из СписокБаз Цикл
		СтруктураАдреса = База.Значение;

		Для каждого Часть Из СтруктураАдреса Цикл
			Если Часть.Ключ = "Name" Тогда
				Данные = Данные + Символы.ВК + Символы.ПС + "[" + Часть.Значение + "]";
			ИначеЕсли Часть.Ключ = "Connect" Тогда
				Данные = Данные + Символы.ВК + Символы.ПС + Часть.Значение.String;
			Иначе
				Если СокрЛП(Часть.Значение) <> "" Тогда
					Данные = Данные + Символы.ВК + Символы.ПС + Часть.Ключ + "=" + Часть.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Данные = Данные + Символы.ВК + Символы.ПС;
	КонецЦикла;

	ЗаписатьРезультатВФайл(ИмяФайлаНастроек, СокрЛП(Данные));

	Возврат Истина;

КонецФункции // ЗаписатьСписокБаз()
